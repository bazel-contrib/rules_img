load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag", "int_flag", "string_flag")
load("@rules_img//img:image.bzl", "image_manifest")
load("@rules_img//img:layer.bzl", "image_layer")
load("@rules_img//img:push.bzl", "image_push")

image_layer(
    name = "go_layer",
    srcs = {
        "/bin/app": "//:app",
    },
)

image_manifest(
    name = "go_image",
    annotations = {
        "org.opencontainers.image.title": "Go Application with Customizations",
        "org.opencontainers.image.description": "Your description goes here.",
        "org.opencontainers.image.licenses": "Apache-2.0",
    },
    cmd = [
        "--my-flag",
        "value",
    ],
    entrypoint = ["/bin/app"],
    env = {
        "MY_ENV_VAR": "value",
    },
    labels = {
        "org.label-schema.name": "Go Application with Customizations",
        "org.label-schema.description": "Your description goes here.",
    },
    layers = [
        ":go_layer",
    ],
    stop_signal = "SIGKILL",
    user = "0",
)

image_push(
    name = "push",
    build_settings = {
        "registry": ":registry",
        "repo": ":repo",
        "tag": ":tag",
        "copyright_year": ":copyright_year",
        "is_production": ":is_production",
    },
    image = ":go_image",
    registry = "{{.registry}}",
    repository = "{{.repo}}",
    stamp = "enabled",
    tag_list = [
        "{{.tag}}",
        "{{if .STABLE_CONTAINER_VERSION_TAG}}{{.STABLE_CONTAINER_VERSION_TAG}}{{else}}dev{{end}}",
        "copyright-{{.copyright_year}}",
        "{{if .is_production}}production{{else}}development{{end}}",
        "another-tag-just-for-fun",
    ],
)

string_flag(
    name = "registry",
    build_setting_default = "ghcr.io",
)

string_flag(
    name = "repo",
    build_setting_default = "malt3/rules_img/go",
)

string_flag(
    name = "tag",
    build_setting_default = "customization",
)

int_flag(
    name = "copyright_year",
    build_setting_default = 2025,
)

bool_flag(
    name = "is_production",
    build_setting_default = False,
)

build_test(
    name = "test",
    targets = [
        ":go_layer",
        ":go_image",
        ":push",
    ],
)
