load("@bazel_lib//:bzl_library.bzl", "bzl_library")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load(":defs.bzl", "PLATFORMS", "go_to_constraint_value", "offline_bcr", "offline_bundle", "release_files", "release_platform_flag", "source_bundle", "versioned_filename_info", name = "platform_name")

# gazelle:exclude_from_release

package(
    default_visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tool_src_tar",
    srcs = ["//:tool_source_files"],
    out = "rules_img_tool.tar.gz",
    extension = "tar.gz",
    mode = "0755",
    strip_prefix = "/src",
    tags = ["manual"],
)

[
    platform(
        name = name(p),
        constraint_values = [go_to_constraint_value[goname] for goname in p],
    )
    for p in PLATFORMS
]

release_platform_flag(
    name = "release_platform",
    build_setting_default = "linux_amd64",
)

release_files(
    name = "prebuilt_toolchain",
    executable = "@rules_img_tool//cmd/img",
    tags = ["manual"],
)

offline_bundle(
    name = "airgapped",
    distdir_contents = ":prebuilt_toolchain",
)

source_bundle(
    name = "srcs",
    srcs = ["//img/private/release/source_files:release_src_files"],
    overrides = [":prebuilt_toolchain"],
    tags = ["manual"],
)

pkg_tar(
    name = "src_tar",
    srcs = [":srcs"],
    out = "rules_img.tar.gz",
    extension = "tar.gz",
    mode = "0755",
    tags = ["manual"],
)

versioned_filename_info(
    name = "versioned_src_tar",
    src = ":src_tar",
    extension = "tar.gz",
    generate_release_notes = True,
    module_name = "rules_img",
    tags = ["manual"],
)

versioned_filename_info(
    name = "versioned_tool_tar",
    src = ":tool_src_tar",
    extension = "tar.gz",
    module_name = "rules_img_tool",
    tags = ["manual"],
)

offline_bcr(
    name = "bcr",
    src_tars = [
        ":versioned_src_tar",
        ":versioned_tool_tar",
    ],
)

pkg_tar(
    name = "dist_tar",
    srcs = [
        ":prebuilt_toolchain",
        ":versioned_src_tar",
        ":versioned_tool_tar",
    ],
    out = "rules_img-dist.tar",
    mode = "0755",
    tags = ["manual"],
)

bzl_library(
    name = "defs",
    srcs = ["defs.bzl"],
    deps = [
        "//img/private/config:defs",
    ],
)
